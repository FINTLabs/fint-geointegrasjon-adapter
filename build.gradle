buildscript {
    ext {
        springBootVersion = '1.5.21.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.21.0'
    id "no.nils.wsdl2java" version "0.10"

}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'org.springframework.boot'


javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.runtime
    options.windowTitle = 'FINT Geointegrasjon Arkiv Adapter'
    options.memberLevel = JavadocMemberLevel.PRIVATE
    destinationDir = file('./javadocs/')
}


repositories {
    maven {
        url 'https://repo.fintlabs.no/releases'
    }
    mavenCentral()
}

dependencies {
    compile 'org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1'

    // enable extension support for wsdl2java
    wsdl2java 'org.jvnet.jaxb2_commons:jaxb2-basics-runtime:1.11.1'
    wsdl2java 'org.jvnet.jaxb2_commons:jaxb2-basics:1.11.1'

    compile('org.apache.cxf:cxf-rt-frontend-jaxws:3.3.3')
    compile('org.apache.cxf:cxf-rt-transports-http:3.3.3')
    compile('org.apache.cxf:cxf-rt-ws-security:3.3.3')
    compile('org.bouncycastle:bcpkix-jdk15on:1.50')
    compile('org.bouncycastle:bcprov-jdk15on:1.50')
    compile('commons-io:commons-io:2.6')

    compile('com.google.guava:guava:28.0-jre')
    compile('org.apache.commons:commons-lang3:3.9')
    compile('org.apache.jena:jena-arq:3.12.0')
    compile('org.glassfish.jersey.core:jersey-client:2.29')
    compile('org.glassfish.jersey.core:jersey-common:2.29')
    compile('org.jooq:jool-java-8:0.9.14')
    compile('org.projectlombok:lombok:1.18.8')

    // Spring adds validation-api 1.x and hibernate-validator 5.x by default, all three dependencies needed to override.
    // Version 6.1.0 does not work :(
    compile 'javax.validation:validation-api:2.0.1.Final'
    compile 'org.hibernate.validator:hibernate-validator:6.0.17.Final'
    compile 'org.hibernate.validator:hibernate-validator-annotation-processor:6.0.17.Final'

    compile 'no.fint:fint-model-resource:0.4.1'
    compile('no.fint:fint-event-model:3.0.1')
    compile('no.fint:fint-relation-model:1.1.6')
    compile('no.fint:fint-spring-secrets:0.2.2')
    compile('no.fint:fint-sse:2.0.0')

    compile('no.fint:fint-arkiv-case-defaults:3.1.0')
    compile("no.fint:fint-arkiv-resource-model-java:${apiVersion}")
    compile("no.fint:fint-administrasjon-resource-model-java:${apiVersion}")

    compile('org.springframework.hateoas:spring-hateoas')
    runtime('org.springframework.plugin:spring-plugin-core')
    runtime('org.springframework.boot:spring-boot-actuator')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-hateoas')
    compile('org.springframework.boot:spring-boot-starter-web')
    compileOnly('org.springframework.boot:spring-boot-configuration-processor')

    compile('com.azure:azure-storage-blob:12.1.0')
    compile 'io.projectreactor:reactor-core:3.3.1.RELEASE'

    testCompile('cglib:cglib-nodep:3.2.12')
    testCompile('org.spockframework:spock-spring:1.1-groovy-2.4')
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0')

    compile 'commons-beanutils:commons-beanutils:1.9.4'
    compile 'org.apache.commons:commons-text:1.8'
}

task copyExternalDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/external"
    exclude '**/fint-*.jar'
}

task copyFintDependencies(type: Copy) {
    from configurations.runtime
    into "$buildDir/deps/fint"
    include '**/fint-*.jar'
}

build.dependsOn(copyExternalDependencies)
build.dependsOn(copyFintDependencies)

jar {
    manifest {
        attributes(
                "Manifest-Version": "1.0",
                "Main-Class": "no.fint.Application",
                "Specification-Version": "${apiVersion}",
                "Implementation-Version": "${version}",
                "Class-Path": configurations.runtime.collect { it.getName() }.join(' ')
        )
    }
}

sourceCompatibility = 1.8

import org.apache.tools.ant.filters.ReplaceTokens

afterEvaluate {
    configure(processResources) {
        filesMatching('**/*.yml') {
            filter(ReplaceTokens, tokens: [version: jar.version, title: jar.baseName])
        }
    }
}

wsdl2java {
    generatedWsdlDir = file('generated-sources')
    wsdlDir = file("$projectDir/src/main/resources/")
    locale = Locale.ENGLISH
    wsdlsToGenerate = [
            ['-xjc-XtoString', '-client', '-p', 'no.geointegrasjon.arkiv.innsyn', "http://rep.geointegrasjon.no/Arkiv/Innsyn/xml.wsdl/2012.01.31/giArkivInnsyn20120131.wsdl"],
            ['-xjc-XtoString', '-client', '-p', 'no.geointegrasjon.arkiv.oppdatering', "http://rep.geointegrasjon.no/Arkiv/Oppdatering/xml.wsdl/2012.01.31/giArkivOppdatering20120131.wsdl"],
    ]
}

wsdl2javaExt {
    cxfVersion = '3.3.3'
    deleteGeneratedSourcesOnClean = true
}

bootRepackage.enabled = false

bootRun {
    systemProperties System.properties
}

test {
    testLogging {
        events 'passed', 'skipped', 'failed'
    }
}

apply from: 'https://raw.githubusercontent.com/FINTlibs/fint-buildscripts/master/dependencyReport.gradle'
